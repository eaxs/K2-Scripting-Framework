// Check if NPC module exists and if any npcs have been created
if [__npc_idx >= 0] #VOID#;
else #@# "Node::__destruct{";

// Loop through each node
#$# node_pet_node_i 0
@Node::__pet__foreach_node{
    // Get node properties
    #$# node_pet_node #__nodes_|#node_pet_node_i|##;
    #$# node_pet_node_d #|#node_pet_node|#_d#;
    #$# node_pet_node_t #|#node_pet_node|#_t#;
    #$# node_pet_node_x #|#node_pet_node|#_x#;
    #$# node_pet_node_y #|#node_pet_node|#_y#;
    #$# node_pet_node_z #|#node_pet_node|#_z#;

    // Check if destination is set
    if #StringLength(|#node_pet_node_d|#)# #VOID#;
    else #@# "Node::__destruct{";

    // Set to cached index
    #$# node_pet_i #__npc_alive#;

    // Loop through each NPC
@Node::__pet__foreach_pet{
        #$# node_pet_e #__npc_|#node_pet_i|##;
        #$# node_pet_ispet #|#node_pet_e|#_isPet#;
        #$# node_pet_isalive #|#node_pet_e|#_isAlive#;
        #$# node_pet_idx #|#node_pet_e|#_idx#;
        #$# node_pet_m #|#node_pet_e|#_mode#;
        #$# node_pet_t #|#node_pet_e|#_team#;
        #$# node_pet_dest #|#node_pet_e|#_dest#;
    
        // Weed out dead pets and npcs
        if #node_pet_isalive# #VOID#;
        else #@# "Node::__pet__foreach_pet_continue{";
        if #node_pet_ispet# #VOID#;
        else #@# "Node::__pet__foreach_pet_continue{";
        
        // Check if npc already on the way to dest
        if #StringEquals(|#node_pet_dest|#,|#node_pet_node_d|#)# #@# "Node::__pet__foreach_pet_continue{";
    
        // Check for team param
        if #StringLength(|#node_pet_node_t|#)# #VOID#;
        else #@# "Node::__pet__foreach_pet_skip_team{";
    
        if [node_pet_t == node_pet_node_t] #VOID#;
        else #@# "Node::__pet__foreach_pet_continue{";
@Node::__pet__foreach_pet_skip_team{

        // Get X and Y coordinates of the pet
        #$# node_pet_x #GetPosX(|#node_pet_idx|#)#
        #$# node_pet_y #GetPosY(|#node_pet_idx|#)#
    
        // Calculate distance from waypoint
        if [node_pet_x > node_pet_node_x] #$# node_pet_dist_x [node_pet_x - node_pet_node_x];
        else #$# node_pet_dist_x [node_pet_node_x - node_pet_x];
    
        if [node_pet_y > node_pet_node_y] #$# node_pet_dist_y [node_pet_y - node_pet_node_y];
        else #$# node_pet_dist_y [node_pet_node_y - node_pet_y];
    
        // Check if X is in range
        if [node_pet_dist_x <= 50] #VOID#;
        else #@# "Node::__pet__foreach_pet_continue{";
    
        // Check if Y is in range
        if [node_pet_dist_y <= 50] #VOID#;
        else #@# "Node::__pet__foreach_pet_continue{";
    
        // Pet in range route to next waypoint
        SetPetJobPatrol #node_pet_idx# #|#node_pet_node|#_dx# #|#node_pet_node|#_dy# #|#node_pet_node|#_dz#
        #$# #node_pet_e#"_dest" #node_pet_node_d#;
    
@Node::__pet__foreach_pet_continue{
        #$# node_pet_i [node_pet_i + 1];
    if [node_pet_i <= __npc_idx] #@# "Node::__pet__foreach_pet{";
    
@Node::__pet__foreach_node_continue{
    #$# node_pet_node_i [node_pet_node_i + 1]
if [node_pet_node_i <= __nodes_idx] #@# "Node::__pet__foreach_node{";
    